plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id 'java-library'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'maven-publish'
}

ext.junitVersion = "5.3.2"

group 'pl.mlynik'
version '1.0.2'

repositories {
    mavenCentral()
    jcenter()
}

test { useJUnitPlatform() }

configurations {
    ktlint
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"

    testCompile "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testCompile "org.junit.jupiter:junit-jupiter-params:$junitVersion"
    runtime "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    ktlint "com.pinterest:ktlint:0.38.0"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "src/**/*.kt"
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/pinterest/ktlint#usage for more
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    classpath = configurations.ktlint
    main = "com.pinterest.ktlint.Main"
    args "-F", "src/**/*.kt"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_SECRET")
    publications = ['mavenJava']
    pkg {
        repo = 'bintray'
        name = 'ChessEngine'
        version {
            name = '1.0.2'
        }
        licenses = ['MIT']
        userOrg = 'lmlynik'
        vcsUrl = 'https://github.com/lmlynik/ChessEngine.git'
    }
}
